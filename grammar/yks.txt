entity yks {

    constraints test_20_constraint_set  {
        dogru >= 0 && dogru <= 20, "Doğru sayısı en az 0 en çok 20 olabilir".
        yanlis >= 0 && yanlis <= 20, "Yanlış sayısı en az 0 en çok 20 olabilir".
        dogru + yanlis <= 20, "Doğru ve yanlış sayısının toplamı en fazla 20 olabilir".
    }.

    constraints test_40_constraint_set {
        dogru >= 0 && dogru <= 40, "Doğru sayısı en az 0 en çok 40 olabilir".
        yanlis >= 0 && yanlis <= 40, "Yanlış sayısı en az 0 en çok 40 olabilir".
        dogru + yanlis <= 40, "Doğru ve yanlış sayısının toplamı en fazla 40 olabilir".
    }.

    constraints test_80_constraint_set  {
        dogru >= 0 && dogru <= 80, "Doğru sayısı en az 0 en çok 80 olabilir".
        yanlis >= 0 && yanlis <= 80, "Yanlış sayısı en az 0 en çok 80 olabilir".
        dogru + yanlis <= 80, "Doğru ve yanlış sayısının toplamı en fazla 80 olabilir".
    }.


    in diploma_notu[50.0..100.0].
    in daha_once_yerlesti.

    ek_puan := (diploma_notu * 5.0) * (daha_once_yerlesti ? 0.06 : 0.12).

    entity tyt {

        entity tyt_turkce {
            in dogru.
            in yanlis.

            out  tyt_turkce_net := dogru - yanlis * 0.25 given test_40_constraint_set.
        }

        entity tyt_matematik {
            in dogru.
            in yanlis.

            out tyt_matematik_net := dogru - yanlis * 0.25 given test_40_constraint_set.
        }

        entity tyt_fen {
            in dogru.
            in yanlis.

            out tyt_fen_net :=  dogru - yanlis * 0.25 given test_20_constraint_set.
        }

        entity tyt_sosyal {
            in dogru.
            in yanlis.

            out  tyt_sosyal_net := dogru - yanlis * 0.25 given test_20_constraint_set.
        }

        out tyt_net := tyt_turkce_net + tyt_matematik_net + tyt_sosyal_net + tyt_fen_net
            given {
                tyt_turkce_net >= 0.5 && tyt_matematik_net >= 0.5.
            }.
    }

    entity ayt {

        entity ayt_edsos1 {
            title: "EDEBİYAT-SOSYAL BİLİMLER 1".

            in dogru.
            in yanlis.

            out ayt_edsos1_net := dogru - yanlis * 0.25 given test_40_constraint_set.

        }

        entity ayt_sosyal2 {
            title: "SOSYAL BİLİMLER 2".

            in dogru.
            in yanlis.

            out  ayt_sosyal2_net := dogru - yanlis * 0.25 given test_40_constraint_set.
        }

        entity ayt_matematik {
            title: "MATEMATİK".

            in dogru.
            in yanlis.

            out  ayt_matematik_net := dogru - yanlis * 0.25  given test_40_constraint_set.
        }

        entity ayt_fen {
            title: "FEN BİLİMLERİ".

            in dogru.
            in yanlis.

            out  ayt_fen_net := dogru - yanlis * 0.25  given test_40_constraint_set.
        }

    }

    entity ydt {
        entity yabanci_dil {
            in dogru[0..80].
            in yanlis[0..80].

            out  yabanci_dil_net := dogru - yanlis * 0.25  given test_80_constraint_set.
        }
    }

    out say_net := ayt_matematik_net + ayt_fen_net      given { ayt_matematik_net >= 0.5 && ayt_fen_net     >= 0.5. }.
    out ea_net  := ayt_matematik_net + ayt_edsos1_net   given { ayt_matematik_net >= 0.5 && ayt_edsos1_net  >= 0.5. }.
    out soz_net := ayt_edsos1_net + ayt_sosyal2_net     given { ayt_edsos1_net    >= 0.5 && ayt_sosyal2_net >= 0.5. }.
    out dil_net := yabanci_dil_net                      given { yabanci_dil_net >= 0.5. }.

    out tyt_ham := 100.0 + (tyt_net * 400.0 / 120.0).
    out say_ham := ((tyt_ham - 100.0) * 0.4 + (say_net * 5.0) * 0.6) + 100.0.
    out ea_ham  := ((tyt_ham - 100.0) * 0.4 + (ea_net * 5.0) * 0.6) + 100.0.
    out soz_ham := ((tyt_ham - 100.0) * 0.4 + (soz_net * 5.0) * 0.6) + 100.0.
    out dil_ham := ((tyt_ham - 100.0) * 0.4 + (dil_net * 5.0) * 0.6) + 100.0.

    out tyt_yerlestirme := tyt_ham + ek_puan.
    out say_yerlestirme := say_ham + ek_puan.
    out ea_yerlestirme := ea_ham + ek_puan.
    out soz_yerlestirme := soz_ham + ek_puan.
    out dil_yerlestirme := dil_ham + ek_puan.
}

// result := compute yks {
//    tyt_turkce::dogru <- 10.
//    tyt_turkce::dogru <- 10.
//    tyt_turkce::dogru <- 10.
//    tyt_turkce::dogru <- 10.
//    tyt_turkce::dogru <- 10.
//    tyt_turkce::dogru <- 10.
// }
//
// print result.